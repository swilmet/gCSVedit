# Maintainer: SÃ©bastien Wilmet <sebastien.wilmet@uclouvain.be>

_realname=gcsvedit
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.1
pkgrel=1
arch=('any')
pkgdesc="A simple CSV text editor (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "gettext-devel")
options=('strip' 'staticlibs')
license=("GPL")
url="http://gcsvedit.sourceforge.net/"
#install=${_realname}-${CARCH}.install
source=(https://sourceforge.net/projects/${_realname}/files/v${pkgver%.*}/${_realname}-${pkgver}.tar.xz)
sha256sums=('d62c7df22cd62d374f6cb70c0fb119a25f67901bc27c2d7c0604e0ad2f693a9f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  autoreconf -f -i
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
